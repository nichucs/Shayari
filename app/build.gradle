apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    def getVersionCode = {
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
                standardOutput = stdout
            }
            return Integer.parseInt(stdout.toString().trim())
        }
        catch (ignored) {
            return 10;
        }
    }

    def getVersionName = {
        try {
            def stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
            return stdout.toString().trim()
        }
        catch (ignored) {
            return '0.0.1';
        }
    }

    defaultConfig {
        applicationId "cs.nizam.shayari"
        minSdkVersion 10
        targetSdkVersion 23
        versionCode getVersionCode()
        versionName getVersionName()
    }
    buildTypes {
        debug {
            buildConfigField("String", "AD_UNIT_ID", "\"ca-app-pub-3940256099942544/6300978111\"")
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "AD_UNIT_ID", "\"ca-app-pub-6021641109787388/7757494152\"")
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.google.android.gms:play-services-ads:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
}
